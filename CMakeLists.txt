# Set minimum CMake version to avoid configuration problems
cmake_minimum_required(VERSION 3.18.6)
# BLAS::BLAS target is added in version 3.18

# Define the project
project(NNTile VERSION 0.0.1 LANGUAGES CXX)

# Show a path to additional auxiliary CMake scripts
list(APPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake_modules")

# List of options
# Build shared libraries instead of static
option(BUILD_SHARED_LIBS "Build shared libraries instead of static" ON)
option(USE_CUDA "Use CUDA toolkit" ON)
option(USE_CBLAS "Use CPU CBLAS" ON)
set(CBLAS_INT_TYPE "int32_t" CACHE STRING
    "CBLAS_INT_TYPE=int32_t for LP64 and CBLAS_INT_TYPE=int64_t for ILP64")
set(CBLAS_H_NAME "cblas.h" CACHE STRING
    "name of header file containing cblas routines")
option(BUILD_TESTS "Build tests" ON)
option(COVERAGE "Generate code coverage report" OFF)

# Provide options for certain cache variables
set_property(CACHE CMAKE_BUILD_TYPE PROPERTY STRINGS "None" "Debug" "Release"
    "RelWithDebInfo" "MinSizeRel")
set_property(CACHE CBLAS_INT_TYPE PROPERTY STRINGS "int32_t" "int64_t")

# Specify the library
add_library(nntile)

# Set C++ standard for all sources
#set_property(TARGET nntile PROPERTY CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# Check if CUDA is available
set(NNTILE_USE_CUDA OFF)
if(USE_CUDA)
    include(CheckLanguage)
    check_language(CUDA)
    if(CMAKE_CUDA_COMPILER)
        enable_language(CUDA)
        # Get cublas
        find_package(CUDAToolkit >=10.1)
        target_link_libraries(nntile PRIVATE CUDA::cublas)
        set(NNTILE_USE_CUDA ON)
    endif()
endif()

# Get CBLAS
set(NNTILE_USE_CBLAS OFF)
if(USE_CBLAS)
    find_package(BLAS)
    if(BLAS_FOUND)
        target_link_libraries(nntile PRIVATE BLAS::BLAS)
        set(NNTILE_USE_CBLAS ON)
    endif()
endif()

# Get the pkg-config
find_package(PkgConfig REQUIRED)

# Get the StarPU-1.3
pkg_check_modules(StarPU REQUIRED starpu-1.3)
target_link_libraries(nntile PUBLIC ${StarPU_LDFLAGS})
target_include_directories(nntile PUBLIC
    "${CMAKE_SOURCE_DIR}/include"
    ${StarPU_INCLUDE_DIRS}
    )
target_include_directories(nntile PRIVATE
    "${CMAKE_BINARY_DIR}/include"
    "${CMAKE_SOURCE_DIR}/external"
    )

# Configure list of definitions
configure_file("include/nntile/defs.h.in"
    "${CMAKE_BINARY_DIR}/include/nntile/defs.h" @ONLY)

# Configure src/tile/gemm.cc that relies on cblas
configure_file("src/tile/gemm.cc.in"
    "${CMAKE_BINARY_DIR}/src/tile/gemm.cc" @ONLY)

# Add directory with the sources
add_subdirectory("src")

# Add sirectory with headers
add_subdirectory("include")

# Add external sources/headers
add_subdirectory("external")

# Add subdirectory with examples
add_subdirectory("examples")

# Check if code coverage report is needed
if(COVERAGE)
    # Tell user what we are doing here
    message(STATUS "Code coverage report was requested, so option BUILD_TESTS"
        " is ON."
        " The report itself can be generated by \"make coverage\" command.")
    # Enable tests even if they were manually disabled
    set(BUILD_TESTS ON)
    # Use CodeCoverage.cmake from cmake_modules
    include(CodeCoverage)
    # Append coverage flags
    append_coverage_compiler_flags()
    # Setup a target for an overall coverage
    setup_target_for_coverage_lcov(NAME coverage
        EXECUTABLE ctest -j
        EXCLUDE "${CMAKE_SOURCE_DIR}/tests/*"
        LCOV_ARGS --no-external)
endif()

# Add subdirectory with tests
if(BUILD_TESTS)
    include(CTest)
    add_subdirectory("tests")
endif()

